
&НаКлиенте
Процедура Сохранить(Команда)
	
	ПараметрыВыбора = Новый Структура("Токен","");
	
	Если ТаблицаТокенов.Количество() Тогда 
		
		ТекущаяСтрокаТаблицы = Элементы.ТаблицаТокенов.ТекущиеДанные;
		
		ПараметрыВыбора.Токен = ТекущаяСтрокаТаблицы.Токен;
		
    	СохранитьНастройкуВJSON();
		
	КонецЕсли;       
	
	ЭтаФорма.Закрыть(ПараметрыВыбора); 
	
КонецПроцедуры  

&НаКлиенте
Процедура СохранитьНастройкуВJSON() Экспорт
	
	Массив = Новый Массив;
	Для каждого Строка из ТаблицаТокенов Цикл
		Если ЗначениеЗаполнено(Строка.Токен) Тогда 
			Структура = Новый Структура;
			Структура.Вставить("Токен",		Строка.Токен);
			Структура.Вставить("Описание",	Строка.Описание);
			
			Массив.Добавить(Структура); 
		КонецЕсли;
	КонецЦикла;  
	
	СформироватьДвоичныеДанныеПодменJSON(Массив);
		
КонецПроцедуры

&НаСервере
Процедура СформироватьДвоичныеДанныеПодменJSON(СтруктураДанных) 
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);     
	
	Настройка = ПолучитьДвоичныеДанныеИзСтроки(ЗаписьJSON.Закрыть(),КодировкаТекста.UTF8);
	
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта,КлючНастроек,Настройка); 
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючОбъекта  = "MRS_ЗагрузкаДанныхИзФронтСистем";
	КлючНастроек = "СписокТокенов_LKK";

	ВосстановитьНастройки();
	
	Элементы.ТаблицаТокенов.Обновить();
	
КонецПроцедуры 

&НаСервере
Процедура ВосстановитьНастройки()
	
	Попытка
		Настройка = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта,КлючНастроек);
		Если Настройка <> Неопределено Тогда 
			ДанныеJson = ПолучитьСтрокуИзДвоичныхДанных(Настройка,КодировкаТекста.UTF8);
		Иначе
			ДанныеJson = Неопределено;
		КонецЕсли;
	Исключение
		Сообщить(ОписаниеОшибки());
		ДанныеJson = Неопределено;
	КонецПопытки;
	
	Если ДанныеJson <> Неопределено Тогда 
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеДанных = Новый ЧтениеДанных(Настройка);
		ЧтениеJSON.ОткрытьПоток(ЧтениеДанных.ИсходныйПоток());
		СтруктураНастроек = ПрочитатьJSON(ЧтениеJSON,Истина);

		Для каждого Настройка из СтруктураНастроек Цикл  
			НоваяСтрока = ТаблицаТокенов.Добавить();
			НоваяСтрока.Токен	 = XMLЗначение(Тип("Строка"),Настройка.Получить("Токен"));
			НоваяСтрока.Описание = XMLЗначение(Тип("Строка"),Настройка.Получить("Описание"));
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры
 
