Функция СоздатьДокументАктСписанияЕГАИС(ДокументВыпускБлюд, СтруктураПараметров, тблСозданныеДокументы)
	
	// << MRS #VINE-129 ВикторовАГ <Artem.Viktorov@mriyaresort.com> 16.06.25, (на основании непроведенного документа ничего формировать не нужно)
	Если СтруктураПараметров.РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат Истина;
	КонецЕсли;
	// >> MRS #VINE-129 ВикторовАГ <Artem.Viktorov@mriyaresort.com> 16.06.25, (на основании непроведенного документа ничего формировать не нужно)
	
	СтруктураВозврата = MRS_ЗагрузкаЕГАИСФронт.ПолучитьОстаткиМарокДляПроизводства(ДокументВыпускБлюд, тблСозданныеДокументы);
	
	Если НЕ СтруктураВозврата.Свойство("Шапка") Тогда
		Возврат Истина;
	КонецЕсли;
	
	//Условия по акту списания ЕГАИС
	ДопСоединение = "ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктСписанияЕГАИС КАК АктСписанияЕГАИСРегистр1
			|		ПО питТиповыеДокументы.ТиповойДокумент = АктСписанияЕГАИСРегистр1.Ссылка
			|			И (АктСписанияЕГАИСРегистр1.ВидДокумента = &ВидОперацииАктСписанияЕГАИСРегистр1)
			|			И (АктСписанияЕГАИСРегистр1.ПричинаСписания = ЗНАЧЕНИЕ(Перечисление.ПричиныСписанийЕГАИС.питПриготовление))
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
			|		ПО (АктСписанияЕГАИСРегистр1.Ссылка = СтатусыДокументовЕГАИС.Документ)
			|";
	ДопОтбор = "И СтатусыДокументовЕГАИС.Статус = &СтатусЧерновикАктСписанияЕГАИС
	|	И СтатусыДокументовЕГАИС.ДальнейшееДействие1 = &ДальнейшееДействиеЕГАИС";
	
	// Поиск существующего документа "Акт списания ЕГАИС",
	// связанного с текущим "Выпуском блюд". 
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	питТиповыеДокументы.ТиповойДокумент
	|ПОМЕСТИТЬ втДокументыВыпускПродукции
	|ИЗ
	|	РегистрСведений.питТиповыеДокументы КАК питТиповыеДокументы
	|	&ДопСоединение
	|ГДЕ
	|	питТиповыеДокументы.ДокументОбщепит = &ДокументПит
	|	И питТиповыеДокументы.ТиповойДокумент ССЫЛКА Документ.АктСписанияЕГАИС
	|	&ДопОтбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокументыВыпускПродукции.ТиповойДокумент
	|ИЗ
	|	втДокументыВыпускПродукции КАК втДокументыВыпускПродукции
	|";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопОтбор", ДопОтбор);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопСоединение", ДопСоединение);

	Запрос.УстановитьПараметр("ДокументПит", ДокументВыпускБлюд);
	
	Запрос.УстановитьПараметр("СтатусЧерновикАктСписанияЕГАИС", Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.Черновик);
	Запрос.УстановитьПараметр("ДальнейшееДействиеЕГАИС", Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
	Запрос.УстановитьПараметр("ВидОперацииАктСписанияЕГАИСРегистр1", Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Если документ найден, то получаем объект иначе создаем новый.
	Если Выборка.Следующий() Тогда
		ТекДокументОбъект = Выборка.ТиповойДокумент.ПолучитьОбъект();
		ЭтоНовый = Ложь;
	Иначе
		ТекДокументОбъект = Документы.АктСписанияЕГАИС.СоздатьДокумент();
		ЭтоНовый = Истина;
	КонецЕсли;
	
	ПараметрыЗаписи = ОпределитьПараметрыЗаписиТиповогоДокумента(ДокументВыпускБлюд.Проведен, ДокументВыпускБлюд.ПометкаУдаления,
		ТекДокументОбъект.Проведен, ТекДокументОбъект.ПометкаУдаления, Новый Структура);
		
	ТекРежимЗаписиДок = ПараметрыЗаписи.РежимЗаписи;	
	
	Если ПараметрыЗаписи.НужнаУстановкаПометкиУдаления И НЕ ЭтоНовый Тогда
	
		Попытка
			ТекДокументОбъект.ДополнительныеСвойства.Вставить("питРазрешитьЗаписьТиповогоДокумента",Истина);
			ТекДокументОбъект.УстановитьПометкуУдаления(ПараметрыЗаписи.ПометкаУдаления);
			Возврат Истина;
		Исключение
			ТекстОшибки = ТекстОшибки + "
			|Ошибка при установке пометки удаления """ + ТекДокументОбъект.Метаданные().Представление() + """
			| по документу Общепит """+ДокументВыпускБлюд+"""
			|по причине: <"+ОписаниеОшибки()+">";
			Возврат Ложь;
		КонецПопытки;
					
	КонецЕсли;
	
	// Товары
	ТекДокументОбъект.Товары.Очистить();
	ТекДокументОбъект.АкцизныеМарки.Очистить();
	ТекДокументОбъект.Комментарий = СтрШаблон("Создан по данным документа общепита: %1",ДокументВыпускБлюд);
	
	Для Каждого ЭлементСтруктуры ИЗ СтруктураВозврата Цикл
		
		Если ЭлементСтруктуры.Ключ = "Шапка" Тогда
			
			ОрганизацияЕГАИС = ЭлементСтруктуры.Значение;

		ИначеЕсли ЭлементСтруктуры.Ключ = "ДанныеСерииТовара" Тогда
			
			Для Каждого СтрокаТЧ из ЭлементСтруктуры.Значение Цикл
				
				НоваяСтрока = ТекДокументОбъект.Товары.Добавить();
				НоваяСтрока.АлкогольнаяПродукция    = СтрокаТЧ.АлкогольнаяПродукция;
				НоваяСтрока.Номенклатура            = СтрокаТЧ.Номенклатура;
				НоваяСтрока.Справка2                = СтрокаТЧ.Справка2;
				НоваяСтрока.Количество              = СтрокаТЧ.Количество;
				НоваяСтрока.КоличествоУпаковок 		= СтрокаТЧ.Количество;
								
			    НоваяСтрокаАМ = ТекДокументОбъект.АкцизныеМарки.Добавить();
			    НоваяСтрокаАМ.ШтрихкодУпаковки 				= СтрокаТЧ.АкцизнаяМарка;
		        НоваяСтрокаАМ.АкцизнаяМарка 				= СтрокаТЧ.АкцизнаяМарка;
			    НоваяСтрокаАМ.Справка2 						= СтрокаТЧ.Справка2;
				НоваяСтрокаАМ.Количество                    = СтрокаТЧ.Количество;
				НоваяСтрокаАМ.ВыбытиеБутылки				= Истина;
				
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	
	// << MRS #VINE-41 ВикторовАГ <Artem.Viktorov@mriyaresort.com> 25.03.25, (Свертка товаров при автоматическом формировании акта списания ЕГАИС)
	ТекДокументОбъект.Товары.Свернуть("АлкогольнаяПродукция, Номенклатура, Характеристика, Серия, Упаковка, Справка2, ИдентификаторСтроки, СтатусУказанияСерий", "Цена, НС_КоличествоДАЛ, Количество, КоличествоУпаковок, Сумма");
	
	//Для Каждого ТекСтрока Из ТекДокументОбъект.Товары Цикл
		//Если ТекСтрока.Количество <> 0 Тогда
		//	ТекСтрока.Цена = ТекСтрока.Сумма / ТекСтрока.Количество;
		//Иначе                                      
		//	ТекСтрока.Цена = ТекСтрока.Сумма / ТекСтрока.КоличествоУпаковок;
		//КонецЕсли;
		
	//КонецЦикла;
	// >> MRS #VINE-41 ВикторовАГ <Artem.Viktorov@mriyaresort.com> 25.03.25, (Свертка товаров при автоматическом формировании акта списания ЕГАИС)
				
	// Дата документа
	Если НЕ ЗначениеЗаполнено(ТекДокументОбъект.Дата) Тогда
		ТекДокументОбъект.Дата = ДокументВыпускБлюд.Дата;
	КонецЕсли;
	
	ТекДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
	ТекДокументОбъект.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.питПриготовление;
	
	ТекДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	
	Если НЕ ЗначениеЗаполнено(ТекДокументОбъект.Номер) Тогда
		ТекДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	ТекДокументОбъект.Ответственный = ДокументВыпускБлюд.Ответственный;
	
	Попытка
		
		ТекДокументОбъект.Записать(ТекРежимЗаписиДок);
		
	Исключение
		ТекстОшибки = Строка(ТекстОшибки) + "
		|Ошибка записи документа " + ТекДокументОбъект.Метаданные().Представление() + "
		| по документу Общепит " + ДокументВыпускБлюд + "
		|по причине: <" + ОписаниеОшибки()  +">";
		Возврат Ложь;
	КонецПопытки;
	
	// Добавление записи в результирующую таблицу в случае успешной записи документов.
	ДобавитьНовуюЗапись(ДокументВыпускБлюд, ТекДокументОбъект, тблСозданныеДокументы, 0);
	
	Возврат Истина;
	

КонецФункции
